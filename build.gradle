import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: "com.github.ben-manes.versions"

buildscript {
    apply from: 'versions.gradle'
    
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradle_versions"
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'kotlin'

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "$versions.jvmTarget"
        }
    }

    version = 'SNAPSHOT'
    archivesBaseName = "${rootProject.name}-${project.name}"

    dependencies {
        testImplementation "junit:junit:$versions.junit"
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    // disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}